# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=moarvm-git
pkgver=20150219
pkgrel=1
pkgdesc="6model-based VM for NQP and Rakudo Perl 6"
arch=('i686' 'x86_64')
depends=('readline')
makedepends=('clang' 'git' 'llvm' 'make' 'pkg-config')
groups=('perl6')
source=(${pkgname%-git}::git+https://github.com/MoarVM/MoarVM
        dynasm::git+https://github.com/MoarVM/dynasm
        dyncall::git+https://github.com/MoarVM/dyncall
        libuv::git+https://github.com/libuv/libuv
        linenoise::git+https://github.com/MoarVM/linenoise)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
provides=('moarvm')
conflicts=('moarvm')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Copying Git submodules...'
  for _submodule in dynasm dyncall libuv linenoise; do
    rm -rf 3rdparty/$_submodule
    cp -dpr --no-preserve=owner "$srcdir/$_submodule" 3rdparty
  done
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  perl Configure.pl --prefix=/usr \
                    --toolchain=gnu \
                    --compiler=clang \
                    --optimize \
                    --use-readline
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

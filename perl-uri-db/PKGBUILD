# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=perl-uri-db
_srcname=URI-db
pkgver=0.13
pkgrel=1
pkgdesc="Database URIs"
arch=('any')
url="https://metacpan.org/pod/URI::db"
license=('PerlArtistic')
depends=('perl' 'perl-uri-nested')
makedepends=('perl-module-build')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/D/DW/DWHEELER/$_srcname-$pkgver.tar.gz)
sha256sums=('c9d63255e480222e00a0938ade681371afbcc6f813a5769ae2ba0e2a68bd3213')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  msg 'Building...'
  perl Build.PL INSTALLDIRS=vendor
  ./Build installdeps
  ./Build
}

check() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  msg 'Testing...'
  ./Build test
}

package() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  msg 'Installing...'
  ./Build prefix="$pkgdir/usr" install
}

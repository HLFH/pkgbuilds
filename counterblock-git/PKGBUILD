# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterblock-git
pkgver=20150323
pkgrel=1
pkgdesc="Extended functionality for Counterparty"
arch=('any')
depends=('cython2'
         'git'
         'leveldb'
         'libxml2'
         'libxslt'
         'mongodb'
         'perl-image-exiftool'
         'python2'
         'python2-aniso8601'
         'python2-appdirs'
         'python2-colorama'
         'python2-configobj'
         'python2-dateutil'
         'python2-flask'
         'python2-gevent'
         'python2-geventhttpclient-git'
         'python2-gevent-socketio'
         'python2-gevent-websocket'
         'python2-grequests-git'
         'python2-json-rpc'
         'python2-jsonschema'
         'python2-lxml'
         'python2-pillow'
         'python2-prettytable'
         'python2-pycoin'
         'python2-pygeoip'
         'python2-pymongo'
         'python2-python-bitcoinlib'
         'python2-pyzmq'
         'python2-redis'
         'python2-repoze.lru'
         'python2-requests'
         'python2-rfc3987'
         'python2-strict-rfc3339'
         'redis'
         'sqlite'
         'zeromq')
makedepends=('git'
             'python2-setuptools')
optdepends=('armory-git: for armory_utxsvr'
            'counterparty-cli-git: Counterparty server')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterblock"
license=('MIT')
source=(git+https://github.com/CounterpartyXCP/counterblock
        counterblock.conf
        counterblock.logrotate
        counterblock.service
        counterblock-reparse.service)
sha256sums=('SKIP'
            '1a569067a0bb0172d36654f70321fb331733d27bc3c45f22fdfd8767a3b926aa'
            'efc4901376dbb89a506112e92ea257d536a0e7d3f056de12d7dd2e7adf6e14f1'
            '92c829cb42d643072e6fe88eb6f4745fc7a96aef7da412d197d0f2ec9024337e'
            'f8504dce5eb8909f2312765ed777bdbc503b141cf42bf0ac0dc2b64be7485d3d')
provides=('counterblock')
conflicts=('counterblock')
install=counterblock.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1

  msg 'Installing counterblock.conf...'
  #install -Dm 600 "$srcdir/counterblock.conf" "$pkgdir/etc/counterblock/counterblock.conf"
  install -Dm 600 "$srcdir/counterblock.conf" "$pkgdir/etc/conf.d/counterblock"

  msg 'Installing counterblock.service'
  install -Dm 644 "$srcdir/counterblock.service" "$pkgdir/usr/lib/systemd/system/counterblock.service"
  install -Dm 644 "$srcdir/counterblock-reparse.service" "$pkgdir/usr/lib/systemd/system/counterblock-reparse.service"

  msg 'Installing counterblock.logrotate...'
  install -Dm 644 "$srcdir/counterblock.logrotate" "$pkgdir/etc/logrotate.d/counterblock"

  msg 'Installing counterblock directories...'
  install -dm 755 "$pkgdir/usr/share/counterblock"
  install -dm 700 "$pkgdir/var/log/counterblock"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

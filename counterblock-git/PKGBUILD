# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterblock-git
pkgver=20150128
pkgrel=1
pkgdesc="Extended functionality for Counterparty"
arch=('any')
depends=('cython2'
         'git'
         'leveldb'
         'libxml2'
         'libxslt'
         'mongodb'
         'perl-image-exiftool'
         'python2'
         'python2-aniso8601'
         'python2-appdirs'
         'python2-colorama'
         'python2-configobj'
         'python2-dateutil'
         'python2-flask'
         'python2-gevent-git'
         'python2-geventhttpclient-git'
         'python2-gevent-socketio'
         'python2-gevent-websocket'
         'python2-grequests-git'
         'python2-json-rpc'
         'python2-jsonschema'
         'python2-lxml'
         'python2-numpy'
         'python2-pillow'
         'python2-prettytable'
         'python2-pycoin'
         'python2-pygeoip'
         'python2-pymongo'
         'python2-python-bitcoinlib'
         'python2-pyzmq'
         'python2-redis'
         'python2-repoze.lru'
         'python2-requests'
         'python2-rfc3987'
         'python2-strict-rfc3339'
         'python2-vex'
         'redis'
         'sqlite'
         'zeromq')
makedepends=('git')
optdepends=('armory-git: for armory_utxsvr'
            'counterparty-cli-git: Counterparty server')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterblock"
license=('MIT')
source=(git+https://github.com/CounterpartyXCP/counterblock#branch=develop
        counterblock.conf
        counterblock.logrotate
        counterblock.service
        counterblock-reparse.service)
sha256sums=('SKIP'
            '1a569067a0bb0172d36654f70321fb331733d27bc3c45f22fdfd8767a3b926aa'
            'efc4901376dbb89a506112e92ea257d536a0e7d3f056de12d7dd2e7adf6e14f1'
            '92c829cb42d643072e6fe88eb6f4745fc7a96aef7da412d197d0f2ec9024337e'
            'f8504dce5eb8909f2312765ed777bdbc503b141cf42bf0ac0dc2b64be7485d3d')
provides=('counterblock')
conflicts=('counterblock')
install=counterblock.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  vex2 --path "$srcdir/${pkgname%-git}/vex" --python python2 --site-packages --always-copy -mr <<EOF
install -dm 755 "$srcdir/${pkgname%-git}/vex/lib/python2.7/site-packages"
PYTHONPATH="$srcdir/${pkgname%-git}/vex/lib/python2.7/site-packages" \
python setup.py install --prefix="$srcdir/${pkgname%-git}/vex" --optimize=1
cp -dpr --no-preserve=owner "$srcdir/${pkgname%-git}/vex" "$srcdir/${pkgname%-git}/vex.bak"
EOF
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 "$pkgdir/usr/share/counterblock"
  find vex.bak -mindepth 1 -maxdepth 1 -exec \
    cp -dpr --no-preserve=owner '{}' "$pkgdir/usr/share/counterblock" \;

  msg 'Cleaning up Python dist info...'
  find "$pkgdir" -type f -print0 | xargs -0 sed -i "s@$srcdir/${pkgname%-git}/vex@/usr/share/counterblock@g"
  sed -i 's@^VIRTUAL_ENV=.*@VIRTUAL_ENV="/usr/share/counterblock"@' "$pkgdir/usr/share/counterblock/bin/activate"
  sed -i 's@^setenv VIRTUAL_ENV.*@setenv VIRTUAL_ENV "/usr/share/counterblock"@' "$pkgdir/usr/share/counterblock/bin/activate.csh"
  sed -i 's@^set -gx VIRTUAL_ENV.*@set -gx VIRTUAL_ENV "/usr/share/counterblock"@' "$pkgdir/usr/share/counterblock/bin/activate.fish"

  msg 'Installing counterblock.conf...'
  #install -Dm 600 "$srcdir/counterblock.conf" "$pkgdir/etc/counterblock/counterblock.conf"
  install -Dm 600 "$srcdir/counterblock.conf" "$pkgdir/etc/conf.d/counterblock"

  msg 'Installing counterblock.service'
  install -Dm 644 "$srcdir/counterblock.service" "$pkgdir/usr/lib/systemd/system/counterblock.service"
  install -Dm 644 "$srcdir/counterblock-reparse.service" "$pkgdir/usr/lib/systemd/system/counterblock-reparse.service"

  msg 'Installing counterblock.logrotate...'
  install -Dm 644 "$srcdir/counterblock.logrotate" "$pkgdir/etc/logrotate.d/counterblock"

  msg 'Installing counterblock directories...'
  install -dm 755 "$pkgdir/usr/share/counterblock"
  install -dm 700 "$pkgdir/var/log/counterblock"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

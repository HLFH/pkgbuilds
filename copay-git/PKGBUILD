# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=copay-git
pkgver=20150508
pkgrel=1
pkgdesc="Multisignature Bitcoin wallet"
arch=('i686' 'x86_64')
depends=('nodejs')
makedepends=('git' 'npm' 'nodejs-bower' 'nodejs-grunt-cli')
optdepends=('insight-bitcore-git: Bitcoin blockchain API'
            'peerjs-server: peer-to-peer API built on top of WebRTC')
url="https://github.com/bitpay/copay"
license=('MIT')
source=(git+https://github.com/bitpay/copay
        copay.conf
        copay.service)
sha256sums=('SKIP'
            '95f4682736658e8e7d728c6709721206f7275ec41d495b892bbaf0fa2b174329'
            'f097a988f5f9e170a15a62691475bed9ffb87446cca7e4cceaf3608feda31228')
provides=('copay' 'nodejs-copay')
conflicts=('copay' 'nodejs-copay')
options=('!strip')
install=copay.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Creating config.js...'
  cp -dpr --no-preserve=ownership config.template.js config.js
}

build() {
  cd ${pkgname%-git}

  msg 'Fetching NPM dependencies...'
  npm install

  msg 'Fetching Web assets...'
  bower install --allow-root --config.interactive=false

  msg 'Building...'
  grunt --target=dev shell
}

package() {
  cd ${pkgname%-git}

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/copay/README.md

  msg 'Installing appdirs...'
  install -dm 700 $pkgdir/usr/share/copay
  for _appdir in bin          \
                 css          \
                 font         \
                 img          \
                 js           \
                 lib          \
                 node_modules \
                 test         \
                 util; do
    cp -dpr --no-preserve=ownership $_appdir $pkgdir/usr/share/copay/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in API.js             \
                  app.js             \
                  bower.json         \
                  config.js          \
                  config.template.js \
                  copay.js           \
                  Gruntfile.js       \
                  index.html         \
                  karma.conf.js      \
                  package.json; do
    cp -dpr --no-preserve=ownership $_appfile $pkgdir/usr/share/copay/$_appfile
  done

  msg 'Installing binaries...'
  for _bin in concat.sh ghpages.sh; do
    install -Dm 755 $_bin $pkgdir/usr/share/copay/$_bin
  done

  msg 'Installing copay.conf...'
  install -Dm 600 $srcdir/copay.conf $pkgdir/etc/conf.d/copay

  msg 'Installing copay.service'
  install -Dm 644 $srcdir/copay.service $pkgdir/usr/lib/systemd/system/copay.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

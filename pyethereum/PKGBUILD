# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=pyethereum
pkgver=0.7.51
pkgrel=1
pkgdesc="Ethereum Python client"
arch=('any')
depends=('leveldb' 'python2' 'python2-bitcoin' 'python2-bottle' 'python2-docopt' 'python2-ethereum-serpent' 'python2-leveldb-svn' 'python2-miniupnpc' 'python2-pysha3' 'python2-pytest' 'python2-repoze.lru' 'python2-six' 'python2-structlog' 'python2-waitress')
makedepends=('python2-setuptools')
groups=('ethereum')
url="https://github.com/ethereum/pyethereum"
license=('MIT')
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/ethereum/$pkgname/tar.gz/$pkgver)
sha256sums=('2390e4b8dd2f820d9e5881f225313ed294ec3444da70badb8fd3c22f19424592')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

building() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=gsl-git
pkgver=20150509
pkgrel=1
pkgdesc="Universal Code Generator"
arch=('i686' 'x86_64')
makedepends=('gcc' 'git' 'make' 'pcre')
url="https://github.com/imatix/gsl"
license=('GPL3')
source=(git+https://github.com/imatix/gsl)
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg2 "Building..."
  make -C src
}

package() {
  cd ${pkgname%-git}

  msg2 'Installing license...'
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/gsl/COPYING"

  msg2 'Installing documentation...'
  install -dm 755 "$pkgdir/usr/share/doc/gsl"
  for _doc in doc/* README.md README.txt; do
    cp -dpr --no-preserve=ownership $_doc "$pkgdir/usr/share/doc/gsl"
  done

  msg2 'Installing...'
  make DESTDIR="$pkgdir/usr" install -C src

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

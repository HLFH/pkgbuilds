# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=cv
pkgdesc="Shows running coreutils basic commands and displays stats"
pkgver=0.5.1
pkgrel=1
arch=('i686' 'x86_64')
depends=('ncurses')
makedepends=('gcc' 'make')
url="https://github.com/Xfennec/cv"
license=('GPL3')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/Xfennec/$pkgname/tar.gz/v$pkgver)
sha256sums=('d390a26a8db78366be8dfee93819135a106610ba7e3f6d94515c84d5803195d4')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Fixing Makefile...'
  sed -i "s@/usr/local@/usr@" Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg 'Installing documentation...'
  for _doc in capture.png README.md; do
    install -Dm 644 "$_doc" "$pkgdir/usr/share/doc/$pkgname/$_doc"
  done

  msg 'Installing...'
  install -dm 755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" install
}

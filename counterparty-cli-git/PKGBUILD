# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterparty-cli-git
pkgver=20150131
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('python'
         'python-appdirs'
         'python-colorlog'
         'python-counterparty-lib-git'
         'python-dateutil'
         'python-prettytable'
         'python-requests')
makedepends=('git'
             'python-setuptools')
optdepends=('bitcoin-core-addrindex: Bitcoin Core headless P2P node with addrindex'
            'bitcoin-headless-addrindex: Bitcoin headless wallet with addrindex'
            'bitcoin-qt-addrindex: Bitcoin-QT wallet with addrindex'
            'ethereum-serpent: compile Serpent contract code')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterparty-cli"
license=('MIT')
options=(!emptydirs)
source=(${pkgname%-git}::git+https://github.com/CounterpartyXCP/counterparty-cli)
sha256sums=('SKIP')
provides=('counterparty-cli' 'counterparty-client' 'counterparty-server')
conflicts=('counterparty-cli' 'counterparty-client' 'counterparty-server')
install=counterparty-cli.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  vex --path "$srcdir/${pkgname%-git}/vex" --always-copy -mr <<EOF
install -dm 755 "$srcdir/${pkgname%-git}/vex/lib/python3.4/site-packages"
PYTHONPATH="$srcdir/${pkgname%-git}/vex/lib/python3.4/site-packages" \
python setup.py install --prefix="$srcdir/${pkgname%-git}/vex" --optimize=1
cp -dpr --no-preserve=owner "$srcdir/${pkgname%-git}/vex" "$srcdir/${pkgname%-git}/vex.bak"
EOF
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 "$pkgdir/usr/share/counterparty-cli"
  find vex.bak -mindepth 1 -maxdepth 1 -exec \
    cp -dpr --no-preserve=owner '{}' "$pkgdir/usr/share/counterparty-cli" \;

  msg 'Cleaning up Python dist info...'
  find "$pkgdir" -type f -print0 | xargs -0 sed -i "s@$srcdir/${pkgname%-git}/vex@/usr/share/counterparty-cli@g"
  sed -i 's@^VIRTUAL_ENV=.*@VIRTUAL_ENV="/usr/share/counterparty-cli"@' "$pkgdir/usr/share/counterparty-cli/bin/activate"
  sed -i 's@^setenv VIRTUAL_ENV.*@setenv VIRTUAL_ENV "/usr/share/counterparty-cli"@' "$pkgdir/usr/share/counterparty-cli/bin/activate.csh"
  sed -i 's@^set -gx VIRTUAL_ENV.*@set -gx VIRTUAL_ENV "/usr/share/counterparty-cli"@' "$pkgdir/usr/share/counterparty-cli/bin/activate.fish"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterparty-cli-git
pkgver=20150205
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('python'
         'python-appdirs'
         'python-colorlog'
         'python-counterparty-lib-git'
         'python-dateutil'
         'python-prettytable'
         'python-requests')
makedepends=('git'
             'python-setuptools')
optdepends=('bitcoin-core-addrindex: Bitcoin Core headless P2P node with addrindex'
            'bitcoin-headless-addrindex: Bitcoin headless wallet with addrindex'
            'bitcoin-qt-addrindex: Bitcoin-QT wallet with addrindex'
            'ethereum-serpent: compile Serpent contract code')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterparty-cli"
license=('MIT')
options=(!emptydirs)
source=(${pkgname%-git}::git+https://github.com/CounterpartyXCP/counterparty-cli
        setup.py.patch)
sha256sums=('SKIP'
            'a681aed587281aa2d71abd8dab0904f3e0a96c285784db4a98a42b5992485bd9')
provides=('counterparty-cli' 'counterparty-client' 'counterparty-server')
conflicts=('counterparty-cli' 'counterparty-client' 'counterparty-server')
install=counterparty-cli.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Patching setup.py...'
  patch -p1 < "$srcdir/setup.py.patch"
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  python setup.py build
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  python setup.py install --root="$pkgdir" --optimize=1

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

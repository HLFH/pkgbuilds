# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python-counterparty-lib
pkgver=9.50.0
pkgrel=1
pkgdesc="Reference implementation of the Counterparty protocol"
arch=('any')
depends=('python'
         'python-appdirs'
         'python-apsw'
         'python-colorlog'
         'python-crypto'
         'python-dateutil'
         'python-flask'
         'python-flask-httpauth'
         'python-json-rpc'
         'python-pycoin'
         'python-pysha3'
         'python-python-bitcoinlib'
         'python-requests'
         'python-tendo'
         'python-tornado'
         'sqlite')
makedepends=('python-setuptools')
optdepends=('ethereum: compile Solidity contract code'
            'ethereum-serpent: compile Serpent contract code')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterpartyd"
license=('MIT')
options=('!emptydirs')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/CounterpartyXCP/counterpartyd/tar.gz/v$pkgver
        setup.py.patch)
sha256sums=('a163c0f29f2025bd941435b551ca2c43cf82392f61983251ce204ef6d6653404'
            'f23b8262572091363e03e1d3a726711fd4a2eed27458edcd858c4138863096f1')

prepare() {
  cd "$srcdir/counterpartyd-$pkgver"

  msg 'Patching setup.py...'
  patch -p1 < "$srcdir/setup.py.patch"
}

build() {
  cd "$srcdir/counterpartyd-$pkgver"

  msg 'Building...'
  python setup.py build
}

package() {
  cd "$srcdir/counterpartyd-$pkgver"

  msg 'Installing...'
  python setup.py install --root="$pkgdir" --optimize=1
}

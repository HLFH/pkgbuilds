# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python-counterparty-lib
pkgver=9.43.3
pkgrel=1
pkgdesc="Reference implementation of the Counterparty protocol"
arch=('any')
depends=('leveldb'
         'python'
         'python-appdirs'
         'python-apsw'
         'python-colorlog'
         'python-crypto'
         'python-dateutil'
         'python-flask'
         'python-flask-httpauth'
         'python-json-rpc'
         'python-plyvel'
         'python-prettytable'
         'python-pycoin'
         'python-pysha3'
         'python-python-bitcoinlib'
         'python-requests'
         'python-tendo'
         'python-tornado'
         'sqlite')
makedepends=('git' 'python-setuptools')
optdepends=('ethereum-serpent: compile Serpent contract code')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterpartyd"
license=('MIT')
options=(!emptydirs)
source=($pkgname::git+https://github.com/CounterpartyXCP/counterpartyd#commit=8618d6f2ab77e6094c98dd69835de29a4744a559)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  msg 'Fixing setup.py...'
  sed -i 's/^install_serpent()//' setup.py
  sed -i 's/^install_apsw()//' setup.py
}

build() {
  cd $pkgname

  msg 'Building...'
  python setup.py build
}

package() {
  cd $pkgname

  msg 'Installing...'
  python setup.py install --root="$pkgdir" --optimize=1

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

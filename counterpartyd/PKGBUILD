# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterpartyd
pkgver=9.49.3
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('leveldb'
         'python'
         'python-appdirs'
         'python-apsw'
         'python-colorlog'
         'python-crypto'
         'python-dateutil'
         'python-flask'
         'python-flask-httpauth'
         'python-json-rpc'
         'python-plyvel'
         'python-prettytable'
         'python-pycoin'
         'python-pysha3'
         'python-python-bitcoinlib'
         'python-requests'
         'python-tendo'
         'python-tornado'
         'sqlite')
makedepends=('python-sphinx')
optdepends=('bitcoin-core-addrindex: Bitcoin core headless P2P node with addrindex'
            'insight-bitcore: Bitcoin blockchain API')
groups=('counterparty')
url="http://counterparty.io"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/CounterpartyXCP/$pkgname/tar.gz/v$pkgver
        counterpartyd.conf
        counterpartyd.logrotate
        counterpartyd.service)
sha256sums=('d8b1652f21ada0f6855c72d499b1ed2fe8237afde8f76ba4b91c3da8776abbca'
            'adc50cb97b31f7b4bb5d8657322369b7d015364c1561aca14ff29c3286ccc12b'
            '0977538447f05ef133f76784fb88e02410ad4f7b0e01b4525efca1cd39cc15d1'
            '1ab7d2ec9f43887e4cc1971e8493f273f6214528f70565b6358bb98d0b618fc8')
install=counterpartyd.install

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/counterpartyd/LICENSE"

  msg 'Installing user manual...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage "$pkgdir/usr/share/man/man1/$_manpage"
  done

  msg 'Installing documentation...'
  install -dm 755 "$pkgdir/usr/share/doc/counterpartyd"
  cp -dpr --no-preserve=ownership docs ChangeLog.md README.md "$pkgdir/usr/share/doc/counterpartyd"

  msg 'Installing appdirs...'
  install -dm 755 "$pkgdir/usr/share/counterpartyd"
  install -dm 700 "$pkgdir/var/log/counterpartyd"
  for _appdir in lib test; do
    cp -dpr --no-preserve=ownership $_appdir "$pkgdir/usr/share/counterpartyd/$_appdir"
  done

  msg 'Installing appfiles...'
  for _appfile in __init__.py \
                  mainnet_burns.csv \
                  pip-requirements.txt \
                  protocol_changes.json \
                  version.json; do
    install -Dm 644 $_appfile "$pkgdir/usr/share/counterpartyd/$_appfile"
  done

  msg 'Installing executables...'
  for _bin in counterparty-cli.py counterpartyd.py; do
    install -Dm 755 $_bin "$pkgdir/usr/share/counterpartyd/$_bin"
  done

  msg 'Installing counterpartyd.conf...'
  install -Dm 600 "$srcdir/counterpartyd.conf" "$pkgdir/etc/counterpartyd/counterpartyd.conf"

  msg 'Installing counterpartyd.service'
  install -Dm 644 "$srcdir/counterpartyd.service" "$pkgdir/usr/lib/systemd/system/counterpartyd.service"

  msg 'Installing counterpartyd.logrotate...'
  install -Dm 644 "$srcdir/counterpartyd.logrotate" "$pkgdir/etc/logrotate.d/counterpartyd"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

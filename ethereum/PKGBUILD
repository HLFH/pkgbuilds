# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum
pkgver=poc.7
pkgrel=1
pkgdesc="Decentralised Consensus-based Deterministic Transaction Resolution Platform"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'curl' 'crypto++' 'gmp' 'jsoncpp' 'leveldb' 'libjson-rpc-cpp-git' 'miniupnpc' 'ncurses' 'openssl' 'python2' 'qt5-base' 'qt5-declarative' 'qt5-quick1' 'qt5-quickcontrols' 'qt5-webkit' 'readline' 'snappy')
makedepends=('autoconf' 'automake' 'cmake' 'gcc' 'libtool' 'yasm')
groups=('ethereum')
url="https://code.ethereum.org"
license=('GPL')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/ethereum/cpp-$pkgname/tar.gz/poc-7)
sha256sums=('f45fdf399c3d011d1fb3687775fffe8e5623c3ab197e7487e4db6afd40e22465')
provides=('alethzero' 'cpp-ethereum' 'eth' 'ethereum' 'lllc' 'mix' 'neth' 'sc' 'solc' 'testeth' 'third')
conflicts=('alethzero' 'cpp-ethereum' 'elixir' 'eth' 'ethereum-serpent' 'lllc' 'mix' 'neth' 'sc' 'secp256k1' 'solc' 'testeth' 'third')

prepare() {
  cd "$srcdir/cpp-$pkgname-poc-7"

  msg 'Fixing cryptopp...'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha3.h>@#include <cryptopp/sha3.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha.h>@#include <cryptopp/sha.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <ripemd.h>@#include <cryptopp/ripemd.h>@g'

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd "$srcdir/cpp-$pkgname-poc-7"

  msg 'Building...'
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_THREAD_LIBS_INIT=pthread ./
  make
}

package() {
  cd "$srcdir/cpp-$pkgname-poc-7"

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
}

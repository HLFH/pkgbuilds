# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-javascript-git
pkgver=20140317
pkgrel=1
pkgdesc="Vastly improves Vim's JavaScript indentation"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/pangloss/vim-javascript"
license=('custom:vim')
source=(git+https://github.com/pangloss/vim-javascript)
sha256sums=('SKIP')
provides=('vim-javascript')
conflicts=('vim-javascript')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing documentation...'
  install -Dm 644 README.md "$pkgdir/usr/share/doc/vim-javascript/README.md"
  install -Dm 644 CONTRIBUTING.md "$pkgdir/usr/share/doc/vim-javascript/CONTRIBUTING.md"

  msg 'Installing appdirs...'
  install -dm 755 "$pkgdir/usr/share/vim/vimfiles"
  for _appdir in extras ftdetect indent syntax; do
    cp -dpr --no-preserve=ownership $_appdir "$pkgdir/usr/share/vim/vimfiles/$_appdir"
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

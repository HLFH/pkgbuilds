# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-haml-git
pkgver=20140808
pkgrel=1
pkgdesc="Vim runtime files for Haml, Sass, and SCSS"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/tpope/vim-haml"
license=('MIT')
source=(git+https://github.com/tpope/vim-haml)
sha256sums=('SKIP')
provides=('vim-haml')
conflicts=('vim-haml')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing appdirs...'
  install -dm 755 "$pkgdir/usr/share/vim/vimfiles"
  for _appdir in compiler ftdetect ftplugin indent syntax; do
    cp -dpr --no-preserve=ownership $_appdir "$pkgdir/usr/share/vim/vimfiles/$_appdir"
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

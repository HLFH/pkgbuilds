# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python-counterparty-lib-git
pkgver=20150131
pkgrel=1
pkgdesc="Reference implementation of the Counterparty protocol"
arch=('any')
depends=('leveldb'
         'python'
         'python-plyvel'
         'python-vex'
         'sqlite')
makedepends=('git' 'python-setuptools')
optdepends=('ethereum-serpent: compile Serpent contract code')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterpartyd"
license=('MIT')
options=(!emptydirs)
source=(${pkgname%-git}::git+https://github.com/CounterpartyXCP/counterpartyd#branch=develop)
sha256sums=('SKIP')
provides=('python-counterparty-lib')
conflicts=('python-counterparty-lib')
options=('!emptydirs' '!strip')
install=python-counterparty-lib.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing setup.py...'
  sed -i "/cmd.run_command('install_serpent')/d" setup.py
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  vex --path "$srcdir/${pkgname%-git}/vex" --always-copy -mr <<EOF
install -dm 755 "$srcdir/${pkgname%-git}/vex/lib/python3.4/site-packages"
PYTHONPATH="$srcdir/${pkgname%-git}/vex/lib/python3.4/site-packages" \
python setup.py install --prefix="$srcdir/${pkgname%-git}/vex" --optimize=1
cp -dpr --no-preserve=owner "$srcdir/${pkgname%-git}/vex" "$srcdir/${pkgname%-git}/vex.bak"
EOF
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 "$pkgdir/usr/share/counterparty-lib"
  find vex.bak -mindepth 1 -maxdepth 1 -exec \
    cp -dpr --no-preserve=owner '{}' "$pkgdir/usr/share/counterparty-lib" \;

  msg 'Cleaning up Python dist info...'
  find "$pkgdir" -type f -print0 | xargs -0 sed -i "s@$srcdir/${pkgname%-git}/vex@/usr/share/counterparty-lib@g"
  sed -i 's@^VIRTUAL_ENV=.*@VIRTUAL_ENV="/usr/share/counterparty-lib"@' "$pkgdir/usr/share/counterparty-lib/bin/activate"
  sed -i 's@^setenv VIRTUAL_ENV.*@setenv VIRTUAL_ENV "/usr/share/counterparty-lib"@' "$pkgdir/usr/share/counterparty-lib/bin/activate.csh"
  sed -i 's@^set -gx VIRTUAL_ENV.*@set -gx VIRTUAL_ENV "/usr/share/counterparty-lib"@' "$pkgdir/usr/share/counterparty-lib/bin/activate.fish"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

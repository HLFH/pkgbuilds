# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-buildozer
pkgver=0.17
pkgrel=1
pkgdesc="Generic Python packager for Android / iOS and Desktop"
arch=('any')
depends=('python2')
makedepends=('cmake' 'libusb' 'python2-setuptools')
url="https://github.com/kivy/buildozer"
license=('MIT')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/b/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz
        android.py.patch
        ios.py.patch)
md5sums=('109cc9cc26bb128f28fc355f49722ae3'
         '99636463a19fe119a0adc45949ce90ab'
         '2d0d0b849d6ed79b24191ff6f3b43f0f')
sha256sums=('67ef1dc5574026723f970e3dcebdeed70815bdb84156d0313e2aebed64f3164c'
            '1b4797d874b8c662698bbdc8bac87156bf836440bd51c0bf6d66bf88586ee480'
            '22b54ca3c5fe07a13fe8a905cd27ffef5db37864ad3b9cae940dab92bf3e5704')
provides=('buildozer' 'python2-buildozer')

prepare(){
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Fixing Cython version...'
  patch -p1 < ${srcdir}/android.py.patch
  patch -p1 < ${srcdir}/ios.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}

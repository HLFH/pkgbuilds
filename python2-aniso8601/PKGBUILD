# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-aniso8601
pkgver=0.84
pkgrel=1
pkgdesc="A library for parsing ISO 8601 strings"
arch=('any')
depends=('python2')
makedepends=('python2-setuptools')
url="https://bitbucket.org/nielsenb/aniso8601"
license=('GPL3')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/a/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz)
md5sums=('fd981c47cdecd930214b9c6e3230966b')
sha256sums=('59d758fdf47b127c9b6f673395d4cd60a21063d6b3737acd5e818c13bb6a445d')

build() {
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}

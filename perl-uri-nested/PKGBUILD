# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=perl-uri-nested
_srcname=URI-Nested
pkgver=0.10
pkgrel=1
pkgdesc="Nested URIs"
arch=('any')
url="https://metacpan.org/pod/URI::Nested"
license=('PerlArtistic')
depends=('perl' 'perl-uri')
makedepends=('perl-module-build')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/D/DW/DWHEELER/$_srcname-$pkgver.tar.gz)
sha256sums=('e1971339a65fbac63ab87142d4b59d3d259d51417753c77cb58ea31a8233efaf')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  msg 'Building...'
  perl Build.PL INSTALLDIRS=vendor
  ./Build installdeps
  ./Build
}

check() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  msg 'Testing...'
  ./Build test
}

package() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  msg 'Installing...'
  ./Build prefix="$pkgdir/usr" install
}

# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterparty-cli
pkgver=1.0.0.RC2
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('python'
         'python-appdirs'
         'python-colorlog'
         'python-counterparty-lib'
         'python-dateutil'
         'python-prettytable'
         'python-requests')
makedepends=('python-certifi'
             'python-setuptools')
optdepends=('bitcoin-core-addrindex: Bitcoin Core headless P2P node with addrindex'
            'bitcoin-headless-addrindex: Bitcoin headless wallet with addrindex'
            'bitcoin-qt-addrindex: Bitcoin-QT wallet with addrindex'
            'ethereum-serpent: compile Serpent contract code')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterparty-cli"
license=('MIT')
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/CounterpartyXCP/$pkgname/tar.gz/v1.0.0-RC2)
sha256sums=('a1e8bfc7b9c1e754359a485d5b054e9fb3de06442d75e88421fb1a08b7120ceb')
provides=('counterparty-cli' 'counterparty-client' 'counterparty-server')
conflicts=('counterparty-client' 'counterparty-server')
install=counterparty-cli.install

prepare() {
  cd "$srcdir/$pkgname-1.0.0-RC2"

  msg 'Fixing setup.py...'
  sed -i 's/^import counterparty/import counterpartylib/' setup.py
}

build() {
  cd "$srcdir/$pkgname-1.0.0-RC2"

  msg 'Building...'
  python setup.py build
}

package() {
  cd "$srcdir/$pkgname-1.0.0-RC2"

  msg 'Installing...'
  python setup.py install --root="$pkgdir" --optimize=1
}

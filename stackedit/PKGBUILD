# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=stackedit
pkgver=4.3.11
pkgrel=1
pkgdesc="Free, open-source Markdown editor based on PageDown, the Markdown library used by Stack Overflow and the other Stack Exchange sites"
arch=('any')
depends=('nodejs')
makedepends=('git' 'nodejs-bower' 'nodejs-gulp' 'npm')
url="https://stackedit.io"
license=('Apache')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/benweet/$pkgname/tar.gz/v$pkgver
        stackedit.service)
sha256sums=('38567b5826d150207129d08ac971548162be23d8db10ed300644c05b9ba97f4f'
            'f40f7bce2f461727762f314b46dee19bad9912dd284cacc2632c9129a1b08807')
options=('!strip')
install=stackedit.install

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Fetching NPM dependencies...'
  npm install --python=python2

  msg2 'Fetching Web assets...'
  bower install --allow-root --config.interactive=false

  msg2 'Compiling Web assets...'
  gulp
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  install -dm 700 "$pkgdir/usr/share/stackedit"
  find . -mindepth 1 -maxdepth 1 -exec \
    cp -dpr --no-preserve=ownership '{}' "$pkgdir/usr/share/stackedit" \;

  msg2 'Installing stackedit.service...'
  install -Dm 644 "$srcdir/stackedit.service" "$pkgdir/usr/lib/systemd/system/stackedit.service"

  msg2 'Installing documentation...'
  install -Dm 644 README.md "$pkgdir/usr/share/doc/stackedit/README.md"
  mv "$pkgdir/usr/share/stackedit/doc" "$pkgdir/usr/share/doc/stackedit"

  msg2 'Installing license...'
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/stackedit/LICENSE.txt"

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

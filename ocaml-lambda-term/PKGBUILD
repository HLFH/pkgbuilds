# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Taylor Venable <taylor@metasyntax.net>

pkgname="ocaml-lambda-term"
pkgver=1.8
pkgrel=1
pkgdesc="Terminal manipulation library for OCaml"
arch=('i686' 'x86_64')
url="http://forge.ocamlcore.org/projects/lambda-term/"
license=('BSD')
depends=('ocaml' 'ocaml-camomile' 'ocaml-lwt' 'ocaml-react' 'ocaml-zed')
makedepends=('ocaml-findlib')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/diml/${pkgname##ocaml-}/tar.gz/$pkgver)
options=('!strip')
sha256sums=('ec5d2e29263444e0922dd559860d13f977cf4c391c5cf959826b158c480fc143')

build() {
  cd "${srcdir}/${pkgname##ocaml-}-${pkgver}"

  msg 'Building...'
  ./configure \
    --prefix=/usr \
    --destdir="$pkgdir" \
    --sbindir=/usr/bin
  env DESTDIR="$pkgdir" \
      OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
      make
      make doc
}


package() {
  cd "${srcdir}/${pkgname##ocaml-}-${pkgver}"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg 'Installing...'
  mkdir -p "$pkgdir/$(ocamlfind printconf destdir)"
  mkdir -p "$pkgdir/$(ocamlfind printconf destdir)/stublibs"
  env DESTDIR="$pkgdir" \
      OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
      make install
}

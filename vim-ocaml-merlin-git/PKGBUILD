# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-ocaml-merlin-git
pkgver=20141002
pkgrel=1
pkgdesc="Context sensitive completion for OCaml in Vim and Emacs"
arch=('i686' 'x86_64')
depends=('ocaml' 'ocaml-findlib' 'ocaml-yojson' 'python2' 'vim')
url="https://github.com/the-lambda-church/merlin"
license=('MIT')
source=(${pkgname%-git}::git+https://github.com/the-lambda-church/merlin)
sha256sums=('SKIP')
options=('!strip')
provides=('vim-ocaml-merlin')
conflicts=('ocaml-merlin-git' 'vim-bufsync-git' 'vim-ocaml-merlin')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Building...'
  install -dm 755 "$pkgdir/usr/bin"
  ./configure \
    --bindir "/usr/bin" \
    --sharedir "/usr/share" \
    --prefix "$pkgdir/usr" \
    --vimdir "$pkgdir/usr/share/vim/vimfiles" \
    --enable-compiled-emacs-mode
  make

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Fixing directories...'
  install -dm 755 "$pkgdir/tmp"
  mv "$pkgdir/usr/share/vim" "$pkgdir/tmp"
  mv "$pkgdir/$pkgdir/usr/share" "$pkgdir/usr"
  mv "$pkgdir/tmp/vim" "$pkgdir/usr/share" && rmdir "$pkgdir/tmp"
  mv "$pkgdir/$pkgdir/usr/bin" "$pkgdir/usr"

  msg 'Installing documentation...'
  for _doc in `find . -maxdepth 1 -type f -name "*.md" -printf '%f\n'` CHANGELOG; do
    install -Dm 644 $_doc "$pkgdir/usr/share/doc/${pkgname%-git}/$_doc"
  done

  msg 'Installing license...'
  install -Dm 644 LICENSE_MIT.txt "$pkgdir/usr/share/licenses/${pkgname%-git}/LICENSE_MIT.txt"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  rm -rf "$pkgdir/$HOME"
}

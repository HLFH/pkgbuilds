# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=athame-git
pkgver=20150408
pkgrel=1
pkgdesc="Patches GNU Readline to embed Vim in Bash, GDB, Python, etc"
arch=('i686' 'x86_64')
depends=('glibc' 'ncurses' 'vim')
makedepends=('gcc' 'git' 'make')
url="https://github.com/ardagnir/athame"
license=('GPL3')
source=(git+https://github.com/ardagnir/athame
        vimbed::git+https://github.com/ardagnir/vimbed)
sha256sums=('SKIP' 'SKIP')
provides=('athame' 'readline')
conflicts=('athame' 'readline')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg2 'Fetching Git submodules...'
  git submodule init
  git config submodule.vimbed.url "$srcdir/vimbed"
  git submodule update

  msg2 'Fixing Makefile.in...'
  sed -i 's@$(libdir)/athame@$(DESTDIR)$(libdir)/athame@g' Makefile.in
}

build() {
  cd ${pkgname%-git}

  # build with -fPIC for x86_64 (FS#15634)
  [[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

  msg2 'Building...'
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/athame \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/athame \
    --localstatedir=/var/lib/athame
  make SHLIB_LIBS=-lncurses
}

package() {
  cd ${pkgname%-git}

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-ocp-indent
pkgver=1.4.2b
pkgrel=1
pkgdesc="Indentation tool for OCaml, to be used from editors like Emacs and Vim"
arch=('any')
depends=('vim')
makedepends=('ocp-build')
groups=('vim-plugins')
url="https://github.com/OCamlPro/ocp-indent"
license=('custom:LGPL3')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/OCamlPro/ocp-indent/tar.gz/$pkgver)
sha256sums=('eb3e2005eadcc8409416695044156ec085a254f819d32efa20c5cd42d91716dc')
install=vimdoc.install

build() {
  cd "$srcdir/${pkgname##vim-}-$pkgver"

  msg 'Building...'
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/vim-ocp-indent \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/vim-ocp-indent \
    --localstatedir=/var/lib/vim-ocp-indent
  make
}

package() {
  cd "$srcdir/${pkgname##vim-}-$pkgver"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/vim-ocp-indent/LICENSE"

  msg 'Installing documentation...'
  install -Dm 644 README.md "$pkgdir/usr/share/doc/vim-ocp-indent/README.md"
  install -Dm 644 CHANGELOG "$pkgdir/usr/share/doc/vim-ocp-indent/CHANGELOG"

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
}

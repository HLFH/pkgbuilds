# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterwallet-git
pkgver=20141202
pkgrel=1
pkgdesc="Deterministic client-side Bitcoin Web wallet for Counterparty"
arch=('any')
makedepends=('git' 'nodejs-bower' 'nodejs-grunt-cli')
optdepends=('counterblockd-git: extended API services'
            'openresty-dev: web app server')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterwallet"
license=('CDDL')
source=(git+https://github.com/CounterpartyXCP/counterwallet)
sha256sums=('SKIP')
provides=('counterwallet')
conflicts=('counterwallet')
options=('!strip')
install=counterwallet.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Fetching Web assets...'
  cd src && bower install --allow-root --config.interactive=false && cd ..

  msg 'Fetching NPM dependencies...'
  npm install

  msg 'Building the static site...'
  grunt build #--force # You must put a transifex user and password in
                       # $HOME/.transifex in this format: user:password
                       # Use --force to continue.
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 "$pkgdir/srv/http/counterwallet"
  tar -c . | tar -x -C "$pkgdir/srv/http/counterwallet"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

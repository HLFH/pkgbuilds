# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-structlog
pkgver=0.4.2
pkgrel=1
pkgdesc="Structured logging for Python"
arch=('any')
depends=('python2')
makedepends=('python2-setuptools')
url="http://www.structlog.org"
license=('Apache' 'MIT')
options=(!emptydirs)
source=("https://pypi.python.org/packages/source/s/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz"{,.asc})
md5sums=('062cda36069e8573e00c265f451f899e'
         'SKIP')
sha256sums=('45a6e7ba0e1f335822cf24e14b95d9590fe13e9e2a6abe58a099638037e84377'
            'SKIP')

prepare(){
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}

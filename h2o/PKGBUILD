# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=h2o
pkgver=0.9.0
pkgrel=1
pkgdesc="Very fast HTTP server written in C"
arch=('i686' 'x86_64')
depends=('libuv' 'libyaml' 'openssl' 'wslay-git')
makedepends=('cmake' 'make')
url="https://github.com/h2o/h2o"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/h2o/$pkgname/tar.gz/v$pkgver)
sha256sums=('497e4dd297da3ed7206a03af8895fa3ba8bbf2cbfc446c94ec869c7d0afdf7e8')
options=('!strip')
provides=('h2o' 'libh2o')
conflicts=('libh2o')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
  make libh2o
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg 'Installing documentation...'
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
  install -Dm 644 libh2o.a "$pkgdir/usr/lib/libh2o.a"
  cp -dpr --no-preserve=ownership include "$pkgdir/usr"
}

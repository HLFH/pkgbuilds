# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=h2o
pkgver=1.2.0
pkgrel=1
pkgdesc="Optimized HTTP server with support for HTTP/1.x and HTTP/2"
arch=('i686' 'x86_64')
depends=('libuv' 'libyaml' 'openssl' 'wslay')
makedepends=('cmake' 'make')
url="https://github.com/h2o/h2o"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/h2o/$pkgname/tar.gz/v$pkgver)
sha256sums=('09aacd84ea0a53eaffdc8e0c2a2cf1108bea5db81d5859a136221fd67f07833f')
provides=('h2o' 'libh2o')
conflicts=('libh2o')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
  make libh2o
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg 'Installing documentation...'
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
}

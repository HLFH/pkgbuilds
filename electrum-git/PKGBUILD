# Maintainer: RunningDroid <runningdroid AT zoho.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

#
# ThomasV PGP key: gpg --recv-key 6694D8DE7BE8EE5631BED9502BD5824B7F9470E6
# http://bitcoin-otc.com/viewgpg.php?nick=ThomasV
#

pkgname=electrum-git
pkgver=20150126
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('hicolor-icon-theme'
         'python2'
         'python2-ecdsa'
         'python2-pbkdf2'
         'python2-protobuf'
         'python2-pyasn1'
         'python2-pyasn1-modules'
         'python2-pyqt4'
         'python2-qrcode'
         'python2-requests'
         'python2-sip'
         'python2-six'
         'python2-slowaes'
         'python2-socksipy-branch'
         'python2-tlslite'
         'qt4'
         'sip')
makedepends=('gettext'
             'git'
             'python2-pycurl'
             'python2-setuptools')
optdepends=('desktop-file-utils: update desktop icon'
            'gtk-update-icon-cache: update desktop icon'
            'python2-btchip: BTChip hardware wallet support'
            'python2-rpyc: send commands to Electrum Python console from an external script'
            'python2-trezor: TREZOR hardware wallet support'
            'python2-zbar: QR code reading support'
            'xdg-utils: update desktop icon')
url="https://github.com/spesmilo/electrum"
license=('GPL3')
source=(git+https://github.com/spesmilo/electrum#commit=16c72d286c119562a591dc06303dfeefa3ccfa01
        setup.py.patch)
sha256sums=('SKIP'
            '7ae9cca8b03e4c804ba5f1d22fee6d0e62d4ec8030693ed6004afec2a017676f')
install=electrum.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Patching setup.py...'
  patch -p1 < "$srcdir/setup.py.patch"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

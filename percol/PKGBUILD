# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=percol
pkgver=0.0.7
pkgrel=1
pkgdesc="Filter the output of a command interactively, inspired by anything.el for Emacs"
arch=('any')
depends=('python2')
makedepends=('python2-setuptools')
url="https://github.com/mooz/percol"
license=('MIT')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('17b67c09d386f47f5918e0db60340355')
sha256sums=('f2094ad83cb413329c73149abb9ed37b39a00fe938aaa46b4e7d5326cc278404')
provides=('percol' 'python2-percol')
conflicts=('python2-percol')
install=percol.install

prepare(){
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}

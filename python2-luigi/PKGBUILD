# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-luigi
pkgver=1.1.0
pkgrel=1
pkgdesc="Build complex pipelines of batch jobs, with dependency resolution, workflow management, and visualization"
arch=('any')
depends=('python2'
         'python2-pyparsing'
         'python2-python-daemon'
         'python2-snakebite'
         'python2-tornado')
makedepends=('python2-setuptools')
optdepends=('python2-mechanize: for debugging Hadoop jobs more easily')
url="https://github.com/spotify/luigi"
license=('Apache')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/l/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz)
md5sums=('742e9d7acbdceb007c5280b9e9f0f00d')
sha256sums=('634ae7cf6423d121d34b0a53c9c395c540d5533aff324fefb7e2b34f2a575d70')
provides=('luigi' 'python2-luigi')
conflicts=('luigi')

prepare(){
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Adrian Perez <aperez@igalia.com>

pkgname=beancount-hg
pkgver=20150315
pkgrel=1
pkgdesc="Double-Entry Accounting from Text Files"
arch=('i686' 'x86_64')
depends=('python'
         'python-bottle'
         'python-dateutil'
         'python-ply'
         'wget')
makedepends=('mercurial'
             'python-setuptools')
url="https://bitbucket.org/blais/beancount"
license=('GPL2')
options=('!emptydirs')
source=(hg+https://bitbucket.org/blais/beancount)
sha256sums=('SKIP')
provides=('beancount')
conflicts=('beancount')

pkgver() {
  cd ${pkgname%-hg}
  hg log -r tip -T "{date|isodate}" | awk '{print $1}' | sed 's/-//g'
}

build() {
  cd ${pkgname%-hg}

  msg2 'Building...'
  export CFLAGS='-std=gnu99'
  python setup.py build_ext -i
  python setup.py build
}

package() {
  cd ${pkgname%-hg}

  msg2 'Installing...'
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

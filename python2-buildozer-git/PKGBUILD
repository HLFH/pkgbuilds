# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-buildozer-git
pkgver=20150601
pkgrel=1
pkgdesc="Generic Python packager for Android / iOS and Desktop"
arch=('any')
depends=('python2' 'python2-colorama' 'python2-pexpect')
makedepends=('cmake' 'git' 'libusb' 'python2-setuptools')
optdepends=('python2-paramiko: remote builds')
url="https://github.com/kivy/buildozer"
license=('MIT')
source=(${pkgname%-git}::git+https://github.com/kivy/buildozer)
sha256sums=('SKIP')
provides=('buildozer' 'python2-buildozer')
conflicts=('buildozer' 'python2-buildozer')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg2 'Fixing Cython version...'
  find . -type f -print0 | xargs -0 sed -i 's#Cython#Cython2#g'
  find . -type f -print0 | xargs -0 sed -i 's#cython#cython2#g'

  msg2 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg2 'Building...'
  python2 setup.py build
}

package() {
  cd ${pkgname%-git}

  msg2 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Taylor Venable <taylor@metasyntax.net>

pkgname="ocaml-utop"
pkgver=1.14
pkgrel=1
pkgdesc="Universal toplevel for OCaml"
arch=('i686' 'x86_64')
url="http://forge.ocamlcore.org/projects/utop/"
license=('BSD')
depends=('ocaml' 'ocaml-camomile' 'ocaml-lambda-term' 'ocaml-lwt' 'ocaml-react' 'ocaml-zed')
makedepends=('ocaml-findlib')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/diml/${pkgname##ocaml-}/tar.gz/$pkgver)
options=('!strip')
sha256sums=('adfslkjadslfkdsl')

build() {
  cd "${srcdir}/${pkgname##ocaml-}-${pkgver}"

  msg 'Building...'
  ./configure \
    --prefix=/usr \
    --destdir="$pkgdir" \
    --sbindir=/usr/bin
  env DESTDIR="$pkgdir" \
      OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
      make
      make doc
}

package() {
  cd "${srcdir}/${pkgname##ocaml-}-${pkgver}"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg 'Installing...'
  mkdir -p "$pkgdir/$(ocamlfind printconf destdir)"
  mkdir -p "$pkgdir/$(ocamlfind printconf destdir)/stublibs"
  env DESTDIR="$pkgdir" \
      OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
      make install
}

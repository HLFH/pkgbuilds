# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf
pkgver=0.9.4.1
pkgrel=1
pkgdesc="Command-line fuzzy finder"
arch=('i686' 'x86_64')
depends=('ncurses' 'tmux')
makedepends=('git' 'go' 'make')
optdepends=('ctags: fzf && select tags'
            'fish: fish compatible keybindings'
            'git: fzf && checkout git commits, branches'
            'vim-fzf: fzf vim plugin'
            'xdg-utils: fzf && open files'
            'zsh: zsh compatible keybindings')
url="https://github.com/junegunn/fzf"
license=('MIT')
source=($pkgname-0.9.4-1.tar.gz::https://codeload.github.com/junegunn/$pkgname/tar.gz/0.9.4-1
        fzf.fish
        fzf.sh
        fzf.zsh)
sha256sums=('77ca5c50838d522b50fb863dd85cb9e907907b8af6cc378463d3cd5aface2fdb'
            '0b981560aa4d86a4853f5194550a96f8a63503f7fc479fb7a41210b15692cd66'
            '191196356c8d356660bb8cc90e7c7ac6111d96dd326cd1e05b56696c054c0264'
            '6d437b73ad79b8df11ef988cb1b6df4074f379b98234a2e5ff5076dc9771bad2')
install=fzf.install

_fzf=src/github.com/junegunn/fzf

#_no_bash=1 # uncomment to save optional bash keybindings as fzf.sh.pacaur
#_no_bash=2 # uncomment to remove support for bash
#_no_fish=1 # uncomment to save optional fish keybindings as fzf.fish.pacaur
#_no_fish=2 # uncomment to remove support for fish
#_no_zsh=1 # uncomment to save optional zsh keybindings as fzf.zsh.pacaur
#_no_zsh=2 # uncomment to remove support for zsh

build() {
  cd "$srcdir/$pkgname-0.9.4-1"

  msg2 'Building...'
  GOPATH="$srcdir" TMPDIR=/tmp go get -u github.com/junegunn/fzf/...
}

package() {
  cd "$srcdir/$pkgname-0.9.4-1"

  msg2 'Installing documentation...'
  for _doc in install uninstall README.md; do
    install -Dm 644 $_doc "$pkgdir/usr/share/doc/fzf/$_doc"
  done

  msg2 'Installing manual...'
  install -Dm 644 "$srcdir/$_fzf/man/man1/fzf.1" "$pkgdir/usr/share/man/man1/fzf.1"

  msg2 'Installing executables...'
  install -Dm 755 "$srcdir/bin/fzf" "$pkgdir/usr/bin/fzf"
  install -Dm 755 "bin/fzf-tmux" "$pkgdir/usr/bin/fzf-tmux"

  msg2 'Placing optional completion and keybindings...'

  # bash
  if [[ "$_no_bash" == 1 ]]; then
    install -Dm 644 "$srcdir/$_fzf/shell/completion.bash" "$pkgdir/usr/share/bash-completion/completions/fzf.pacaur"
    install -Dm 755 "$srcdir/fzf.sh" "$pkgdir/etc/profile.d/fzf.sh.pacaur"
  elif [[ "$_no_bash" == 2 ]]; then
    msg2 'Removing bash support'
  else
    install -Dm 644 "$srcdir/$_fzf/shell/completion.bash" "$pkgdir/usr/share/bash-completion/completions/fzf"
    install -Dm 755 "$srcdir/fzf.sh" "$pkgdir/etc/profile.d/fzf.sh"
  fi

  # fish
  if [[ "$_no_fish" == 1 ]]; then
    install -Dm 644 "$srcdir/fzf.fish" "$pkgdir/usr/share/fish/functions/fzf.fish.pacaur"
  elif [[ "$_no_fish" == 2 ]]; then
    msg2 'Removing fish support'
  else
    install -Dm 644 "$srcdir/fzf.fish" "$pkgdir/usr/share/fish/functions/fzf.fish"
  fi

  # zsh
  if [[ "$_no_zsh" == 1 ]]; then
    install -Dm 755 "$srcdir/fzf.zsh" "$pkgdir/etc/profile.d/fzf.zsh.pacaur"
  elif [[ "$_no_zsh" == 2 ]]; then
    msg2 'Removing zsh support'
  else
    install -Dm 755 "$srcdir/fzf.zsh" "$pkgdir/etc/profile.d/fzf.zsh"
  fi
}

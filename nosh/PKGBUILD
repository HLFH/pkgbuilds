# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=nosh
pkgver=1.11
pkgrel=1
pkgdesc="Suite of system-level utilities for initializing and running a BSD or Linux system, and for managing daemons - a superset of Bernstein's daemontools mechanism"
arch=('i686' 'x86_64')
makedepends=('gcc' 'redo')
url="http://homepage.ntlworld.com/jonathan.deboynepollard/Softwares/nosh.html"
license=('custom:ISC')
source=(http://homepage.ntlworld.com/jonathan.deboynepollard/Softwares/$pkgname/$pkgname-$pkgver.tar.bz2
        tools.patch)
sha256sums=('416074ddc940984adeef189da8108decdbf8a6577e4b6778a83524ad7b5bcf05'
            'd25f9c660b70b305b07a4a71a14bcd70a59d2ca4cf87faac14d3b10afa924dfe')

prepare() {
  cd "$srcdir"

  msg 'Fixing compile tools...'
  patch -p1 < "$srcdir/tools.patch"

  msg 'Fixing calls...'
  find . -type f -print0 | xargs -0 sed -i "s@sys/event.h@event.h@g"
  find . -type f -print0 | xargs -0 sed -i "s@ncursesw/curses.h@curses.h@g"

  msg 'Fixing export utility...'
  sed -i "s@man/man1@share/man/man1@g" package/export
}

build() {
  cd "$srcdir"

  msg 'Building...'
  ./package/compile
}

package() {
  cd "$srcdir"

  msg 'Installing license...'
  install -Dm 644 source/COPYING "$pkgdir/usr/share/licenses/nosh/COPYING"

  msg 'Installing...'
  ./package/export "$pkgdir/usr/"
}
